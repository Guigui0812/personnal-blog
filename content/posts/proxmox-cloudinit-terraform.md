---
date: '2025-08-23T20:00:00+01:00'
draft: false
title: 'Proxmox : Automatiser le d√©ploiement de VM avec Cloud-Init et Terraform'
categories: ['proxmox', 'terraform', 'cloud-init', 'virtualisation', 'infrastructure as code']
cover:
  image: '/images/proxmox-terraform-cloudinit.png'
  alt: 'Sch√©ma expliquant l‚Äôautomatisation du d√©ploiement de VM sur Proxmox avec Terraform et Cloud-Init'
  caption: 'Proxmox avec Terraform & Cloud Init'
  focalPoint: 'center center'
---

D√©ployer des machines virtuelles sur un hyperviseur tel que `Proxmox` est une t√¢che fr√©quente pour tout administrateur syst√®me. Que ce soit dans un contexte de production ou dans un homelab, il s'agit d'une t√¢che r√©currente. L'automatisation √©tant d√©sormais au coeur des m√©tiers de l'infrastructure, c'est exactement ce type de t√¢che qu'il convient de repenser sous le prisme de **l'infrastructure as code** via des outils modernes tels que `Terraform` et `Cloud-Init`.

## Contexte

Dans mon **homelab personnel**, je suis tr√®s souvent amen√© √† mettre en place de nouvelles machines virtuelles sur mon hyperviseur Proxmox lorsque je souhaite faire des tests ou h√©berger un nouveau service. Cela peut vite devenir fastidieux puisque sans automatisation, je dois cr√©er manuellement une nouvelle machine √† chaque fois. 

D√®s lors, la solution est simple : **automatiser la mise en place de nouvelles machines virtuelles sur l'hyperviseur** pour pouvoir se concentrer sur les tests et le d√©ploiement des nouveaux services, sans passer par l'√©tape suppl√©mentaire de configuration d'un h√¥te suppl√©mentaire.

## Pr√©parer Proxmox pour l'automatisation

Avant d'aller plus avant, il faut pr√©parer l‚Äôenvironnement Proxmox. 

### Activation des snippets

Dans Proxmox, les **snippets sont des fichiers de configuration** (YAML, scripts) stock√©s dans un datastore. Ils permettent d‚Äôautomatiser des actions au moment du provisionnement. C'est donc dans cet emplacement que seront stock√©s les fichiers `Cloud-Init`. Ces derniers **contiendront la configuration des machines virtuelles d√©ploy√©es** afin de disposer de serveurs "cl√©s en main", pr√™ts √† √™tre utilis√©s.

L'activation est √† effectuer en passant par les menus `Datacenter`, `Storage` puis `local` afin de finalement cocher le param√®tre `Snippets`.

### T√©l√©charger une image Cloud-Init

Les distributions majeures maintiennent des images `Cloud-init` pr√™tes √† l‚Äôemploi, compatibles avec l'outil du m√™me nom.

Quelques exemples par distributions : 

- [Ubuntu](https://cloud-images.ubuntu.com/)
- [Debian](https://cloud.debian.org/images/cloud/) (choisir une image `generic`)
- [Fedora](https://fedoraproject.org/fr/cloud/download)

Choisissez une distribution, t√©l√©chargez l'image de votre choix et importez-la dans le r√©pertoire `ISO images` de votre hyperviseur `Proxmox`.

## Initier le projet Terraform

**Pr√©requis** : si `Terraform` n'est pas install√© sur votre machine, installez le en suivant la [documentation](https://developer.hashicorp.com/terraform/install).

Afin d'int√©ragir avec l'API de `Proxmox` via `Terraform`, il faut utiliser un `provider`. Il s'agit du plugin √† l'outil d'int√©ragir avec des services ou plateformes cloud (`Proxmox`, `AWS`, `GCP`,...) en traduisant les fichiers de configuration `HCL` (*Hashicorp Configuration Language*) en appels API √† destination de ces plateformes.

Comme un grand nombre de services modernes, `Proxmox` expose une `API` permettant d'effectuer la plupart des actions qui peuvent √™tre d√©clench√©es via l'interface graphique. D√®s lors, diff√©rents `provider` ont √©t√© d√©velopp√©s par la communaut√© pour interagir avec cette plateforme. 

Parmi ceux-ci, j'ai choisi [bpg/proxmox](https://registry.terraform.io/providers/bpg/proxmox/latest/docs) qui se d√©gage clairement du lot de par ses fonctionnalit√©s, sa documentation et la fr√©quence de ses mises √† jour. C'est donc celui-ci que j'ai d√©cid√© d'utiliser dans le cadre de l'automatisation de mon **Homelab**.


Ajouter le `provider` √† votre fichier `provider.tf` :

```hcl
terraform {
  required_providers {
    proxmox = {
      source  = "bpg/proxmox"
      version = "<version>"
    }
  }
}
```

Ajouter le ensuite le bloc de configuration de `bpg/proxmox` √† `provider.tf` :

```hcl
provider "proxmox" {
    endpoint = "https://10.0.0.2:8006/"

    username = "root@pam"
    password = "<proxmox_password>"

    insecure = true # if self-signed TLS certificate is in use

    ssh {
        agent = true
    }
}
```

Enfin, il ne reste plus qu'√† initier votre projet avec `terraform init`, ce qui permettra d'initialiser le projet `Terraform` : cr√©ation du fichier `tfstate`, t√©l√©chargement des providers, etc.

Pour √©viter de stocker des secrets tels que le mot de passage `Proxmox` dans le code `Terraform`, il est possible de passer par une **variable sensible** :

```hcl
variable "proxmox_password" {
    description = "The password to connect to the Proxmox API"
    type        = string
    sensitive   = true
}
```

Il faut ensuite utiliser cette variable comme valeur du param√®tre `password` dans le `provider` :

```hcl
provider "proxmox" {
    endpoint = "https://10.0.0.2:8006/"

    username = "root@pam"
    password = var.proxmox_password

    insecure = true # if self-signed TLS certificate is in use

    ssh {
        agent = true
    }
}
```

Avant d'effectuer les √©tapes `plan` et `apply`, il faudra cr√©er la variable d'environnement `TF_VAR_proxmox_password` :

```bash
export TF_VAR_proxmox_password=<password>
echo $TF_VAR_proxmox_password # v√©rification
```

`Terraform` r√©cup√©rant l'ensemble des variables d'environnement pr√©fix√©es par `TF_VAR`, la valeur du param√®tre sera celle d√©finie dans la variable.

Le lien entre l'outil `IaC` et l'hyperviseur √©tant maintenant op√©rationnel, il est temps de passer √† l'√©tape de cr√©ation d'une machine virtuelle.

## Cr√©ation d'une machines virtuelle proxmox avec Terraform

Le d√©ploiement d'un serveur simple, sans configuration personnalis√©e, s'av√®re rapide avec `Terraform` et `bpg/proxmox`.

Dans un premier temps, il suffit de cr√©er un fichier `main.tf` (ou autre nom de votre choix) et d'y ajouter le code permettant de cr√©er une machinne virtuelle : 

```hcl
data "local_file" "ssh_public_key" {
  filename = "./id_rsa.pub"
}

resource "proxmox_virtual_environment_vm" "ubuntu_vm" {
  name      = "example-ubuntu"
  node_name = "pve" # Doit correspondre au nom de votre hyperviseur

  initialization {
    ip_config {
      ipv4 {
        address = "<ip>/<cidr>"
        gateway = "<ip>"
      }
    }

    user_account {
      username = "<username>"
      keys     = [trimspace(data.local_file.ssh_public_key.content)]
    }
  }

  disk {
    datastore_id = "local-lvm"
    file_id      = proxmox_virtual_environment_download_file.ubuntu_cloud_image.id
    interface    = "virtio0"
    iothread     = true
    discard      = "on"
    size         = 30
  }

  network_device {
    bridge = "vmbr0"
  }
}

# T√©l√©chargement automatis√© de l'ISO, sans action manuelle
resource "proxmox_virtual_environment_download_file" "ubuntu_cloud_image" {
  content_type = "iso"
  datastore_id = "local"
  node_name    = "pve" # Doit correspondre au nom de votre hyperviseur
  url          = "<url_image_iso>"
}
```

**Remarque** : le fichier `./id_rsa.pub` (cl√© publique) doit √™tre pr√©sent dans votre projet `Terraform`.

Pour d√©ployer le nouveau serveur, il suffit de suivre les √©tapes classiques d'un d√©ploiement `Terraform`:

- Effectuer le **plan** afin de v√©rifier les changements apport√©s √† l'infrastructure : `terraform plan -out <fichier>.plan`
- Appliquer les changements : `terraform apply <fichier>.plan`

Apr√®s quelques instants, la nouvelle machine virtuelle devrait √™tre d√©ploy√©e et en fonctionnement sur l'hyperviseur.

**Remarque** : Si vous souhaitez d√©truire le serveur, √©vitez les actions manuelles car cela pourrait cr√©er une inconsistance dans votre fichier `tfstate`. Effectuez plut√¥t la destruction via `terraform destroy` une fois le code de votre VM comment√© ou en utilisant l'option `-target`.

## Cr√©er une VM avec configuration personnalis√©e

### Exemple de d√©ploiement avec Terraform et Cloud Init

Bien qu'automatiser le d√©ploiement d'une VM sur `Proxmox` permette un l√©ger gain de temps, ce n'est pas suffisant pour parler d'automatisation. Les √©tapes post√©rieures au d√©ploiement comme la configuration d'utilisateurs ou l'installation de paquets restent √† √™tre effectu√©es. 

Heureusement, `bpg/proxmox` permet d'exploiter la capacit√© de `Proxmox` d'utiliser des templates de configuration `Cloud Init` pour appliquer des configurations personnalis√©es aux VMs utilisant des **images cloud** (impossible avec des images standards).

`Cloud Init` est un **outil permettant de d√©finir la configuration de machines virtuelles lors de leur premier d√©marrage**. Via un fichier de configuration au format `YAML`, l'administrateur va pouvoir cr√©er des utilisateurs, installer des paquets, personnaliser des √©l√©ments du serveur et m√™me ex√©cuter des scripts. C'est donc un outil tr√®s **utile pour d√©ployer des serveurs pr√™ts √† l'emploi de mani√®re standardis√©e**. 

La cr√©ation d'une VM utilisant `Cloud Init` avec `Terraform` peut √™tre effectu√©e √† l'aide de l'exemple suivant :

```hcl
resource "proxmox_virtual_environment_file" "example-ubuntu-cloud-config" {
    content_type = "snippets"
    datastore_id = "local"
    node_name    = "pve"

    source_raw {
    data = <<-EOF
    #cloud-config
    hostname: example-ubuntu
    timezone: Europe/Paris
    users:
        - default
        - name: <username>
          sudo: ALL=(ALL) NOPASSWD:ALL
          shell: /bin/bash
          groups:
            - sudo

    expire: False
    ssh_pwauth: true
    runcmd:
        - echo "MOTD_CONTENT" > /etc/motd
    package_update: true
    packages:
      - qemu-guest-agent
      - net-tools
      - curl
    runcmd:
      - systemctl enable qemu-guest-agent
      - systemctl start qemu-guest-agent
    EOF

    file_name = "example-ubuntu-cloud-config.yaml"
  }
}

resource "proxmox_virtual_environment_vm" "example-ubuntu" {
  name      = "example-ubuntu" # Doit √™tre le m√™me √† celui d√©fini dans la configuration cloud-init
  node_name = "pve"

  cpu {
    cores = "2"
  }

  memory {
    dedicated = "2048"
  }

  initialization {
    user_account {
      username = "<username>"
      password = var.proxmox_password
    }

    ip_config {
      ipv4 {
        address = "<ip>/<cidr>"
        gateway = "<ip>"
      }
    }

    user_data_file_id = proxmox_virtual_environment_file.example-ubuntu-cloud-config.id
  }

  disk {
    datastore_id = "local-lvm"
    file_id      = "local:iso/<iso_image_name>.img" # Convertir l'image au format img si n√©cessaire
    interface    = "virtio0"
    iothread     = "true"
    discard      = "on"
    size         = "30"
  }

  network_device {
    bridge = "vmbr0"
  }
}
```

Le d√©ploiement de cette VM s'effectue de la m√™me mani√®re que pr√©c√©demment en suivant les √©tapes de **plan** et **apply**. Apr√®s quelques instants, une nouvelle machine virtuelle sera d√©ploy√©e avec l'ensemble des configurations correspondant √† celles d√©finies dans le bloc d√©di√© √† `Cloud Init`. De nombreuses autres directives sont d'ailleurs disponibles pour pousser la personnalisation.

### Zoom sur Cloud Init

`Cloud Init` propose un grand nombre de directives pour personnaliser diff√©rents aspects des d√©ploiements.

#### Paquets

Parfait üëç Tu peux faire une section **‚ÄúZoom sur Cloud-Init‚Äù** bien d√©coup√©e par th√©matique. Voici une version pr√™te √† int√©grer dans ton article, avec des exemples r√©alistes et comment√©s :

---

### Zoom sur Cloud-Init

`Cloud-Init` propose un grand nombre de directives pour personnaliser diff√©rents aspects des d√©ploiements. L'ensemble des directive est d√©taill√©e dans la [documentation de Cloud Init](https://cloudinit.readthedocs.io/en/latest/reference/index.html).

#### Paquets

Installer automatiquement des paquets au premier boot :

```yaml
#cloud-config
packages:
  - vim
  - htop
  - curl
  - git
```

L'installation des `guest-agents` est d'ailleurs fortement recommand√©e sur vos VMs :

```yaml
#cloud-config
packages:
   - qemu-guest-agent
runcmd:
   - systemctl enable qemu-guest-agent
   - systemctl start qemu-guest-agent
```

#### R√©seau

Configurer une interface r√©seau avec une IP statique :

```yaml
#cloud-config
network:
  version: 2
  ethernets:
    ens18:
      dhcp4: no
      addresses:
        - 192.168.1.50/24
      gateway4: 192.168.1.1
      nameservers:
        addresses: [192.168.1.1, 8.8.8.8]
```

Configurer les `dns`via `resolv.conf` :

```yaml
#cloud-config
manage_resolv_conf: true
resolv_conf:
  domain: example.com
  nameservers: [8.8.8.8, 8.8.4.4]
  options: {rotate: true, timeout: 1}
  searchdomains: [foo.example.com, bar.example.com]
  sortlist: [10.0.0.1/255, 10.0.0.2]
```

#### Utilisateurs

D√©finir des utilisateurs suppl√©mentaires avec sudo et cl√©s SSH :

```yaml
#cloud-config
users:
  - name: username
    groups: sudo
    shell: /bin/bash
    sudo: ALL=(ALL) NOPASSWD:ALL
    ssh_authorized_keys:
      - <ssh_pub_key>
    plain_text_passwd: <password>
```

#### Commandes √† l‚Äôinitialisation

Lancer des commandes apr√®s le boot :

```yaml
#cloud-config
runcmd:
  - echo "Server Initialised" > init.log
```

---

#### Gestion des mots de passe

D√©finir un mot de passe pour un utilisateur et activer l‚Äôauthentification SSH par mot de passe (optionnel) :

```yaml
#cloud-config
chpasswd:
  list: |
    username:motdepasse
  expire: False
ssh_pwauth: true
```

#### Mise √† jour et upgrade syst√®me

Forcer la mise √† jour du syst√®me lors du premier boot :

```yaml
#cloud-config
package_update: true
package_upgrade: true
```

## Standardisation des d√©ploiement via un module d√©di√©

L'utilisation de `Terraform` pour d√©ployer des machines virtuelles dans `Proxmox` m'a apport√© fluidit√© et facilit√© d'utilisation lors de la mise en place de nouveaux services dans mon Homelab. Cependant, si dans un premier temps j'effectuais une copie de mes configurations pour cr√©er un nouveau serveur, j'ai rapidement pr√©f√©r√© cr√©er un module `Terraform` afin de standardiser mes d√©ploiements et de disposer d'un bloc de code r√©utilisable afin d'√©viter ces √©tapes fastidieuses. 

Ce module est d'ailleurs disponible publiquement sur [github](https://github.com/Guigui0812/Terraform-Proxmox-Module/tree/main) afin d'aider des personnes qui souhaiteraient d√©ployer facilement des VMs dans leur mon homelab personnel.

## Conclusion

Cette approche permet de d√©ployer rapidement des VMs pr√™tes √† l‚Äôemploi. L‚Äô√©tape suivante, dans mon cas, a √©t√© d‚Äôint√©grer Ansible pour configurer automatiquement les services au-dessus des VMs. Cela permet de pousser l‚Äôautomatisation encore plus loin, dans une logique compl√®te d‚ÄôInfrastructure as Code.

## R√©f√©rences

- [Documentation de bpg/proxmox](https://registry.terraform.io/providers/bpg/proxmox/latest/docs)
- [Proxmox : d√©ployer une machine virtuelle KVM avec cloud-init ‚Äì memo-linux.com](https://memo-linux.com/proxmox-deployer-une-machine-virtuelle-kvm-avec-cloud-init/)
- [Cr√©er des templates de machines virtuelles avec Cloud-Init](https://www.bejean.eu/2023/03/24/creer-des-templates-de-vm-avec-cloud-init)
- [Provisionner des VM avec Terraform - St√©phane Roberts](https://blog.stephane-robert.info/docs/virtualiser/type1/proxmox/terraform/)
- [Terraform Apply - When to Run & Quick Usage Examples](https://spacelift.io/blog/terraform-apply)
- [Instancier une machine virtuelle Debian dans Proxmox avec OpenTofu et¬†Cloud-Init](https://xieme-art.org/post/instancier-une-machine-virtuelle-debian-dans-proxmox-avec-opentofu-et-cloud-init/)
- [cloud-init 25.1 documentation](https://cloudinit.readthedocs.io/en/latest/index.html)

